dependencies {
    implementation(project(":agent-bridge"))
    implementation(project(":agent-bridge-datastore"))

    implementation("com.amazonaws:aws-java-sdk-dynamodb:1.11.106")

    testImplementation("com.amazonaws:DynamoDBLocal:1.11.0")
    testImplementation("com.almworks.sqlite4java:sqlite4java:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-osx:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-linux-i386:1.0.392")
    testImplementation("com.almworks.sqlite4java:libsqlite4java-linux-amd64:1.0.392")
}

repositories {
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

jar {
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.aws-java-sdk-dynamodb-1.11.106' }
}

verifyInstrumentation {
    /*
        This is the original verifier range prior to this update.
        Previously it was simply "passes"; this changes it to
        the standard "passOnly" method.
     */
    passesOnly 'com.amazonaws:aws-java-sdk-dynamodb:[1.11.106,)'

    /*
        Now, exclude the ranges we know have passed verification
        previously. We set the range such that the last revision
        of each minor version will still get verified.

        For the last known minor version, we leave the verify
        range open ended to catch any new versions released
        (including any new major/minor versions). The verify
        ranges will probably need to be manually inspected
        every so often to keep the task runtime in check.

        This dramatically reduces the runtime of the verification
        task. For this module, there were over 1500 artifacts that
        were being downloaded for each verification - AWS seems to
        enjoy releasing new revisions of SDK artifacts every few days.

        This pattern can be applied to any module that suffers from
        large numbers of artifacts (looking at anything AWS related).
     */
    exclude 'com.amazonaws:aws-java-sdk-dynamodb:[1.9.0,1.9.39]'
    exclude 'com.amazonaws:aws-java-sdk-dynamodb:[1.10.0,1.10.76]'
    exclude 'com.amazonaws:aws-java-sdk-dynamodb:[1.11.0,1.11.104]'
    exclude 'com.amazonaws:aws-java-sdk-dynamodb:[1.11.106,1.11.1033]'
    exclude 'com.amazonaws:aws-java-sdk-dynamodb:[1.12.0,1.12.791]'
}

// copy the native files
task copyNativeDeps(type: Copy) {
    from(configurations.testCompileClasspath) {
        include "*.so"
        include "*.dylib"
    }
    into "build/nr-native-libs"
}

test {
    dependsOn copyNativeDeps
    jvmArgs(["-Dsqlite4java.library.path=build/nr-native-libs"])
}

site {
    title 'AWS DynamoDB'
    type 'Datastore'
}
