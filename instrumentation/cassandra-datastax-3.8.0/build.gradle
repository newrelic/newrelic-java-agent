jar {
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.cassandra-datastax-3.8.0' }
}

dependencies {
    implementation(project(":agent-bridge"))
    implementation(project(":agent-bridge-datastore"))
    implementation(project(":newrelic-api"))
    implementation(project(":newrelic-weaver-api"))
    implementation("com.google.guava:guava:19.0") { transitive = false }
    implementation("com.datastax.cassandra:cassandra-driver-core:3.8.0") { transitive = false }

    testImplementation("com.github.jbellis:jamm:0.3.2")
    testImplementation("org.cassandraunit:cassandra-unit:3.11.2.0")
}

// This pulls in the cross agent test json files so we can test cassandra obfuscation here
task copySqlObfuscationTest(type: Copy) {
    from new File(rootProject.rootDir, 'newrelic-agent/src/test/resources/com/newrelic/agent/cross_agent_tests/sql_obfuscation/')
    into file('build/sql_obfuscation_test')
}

sourceSets.test.resources.srcDir 'build/sql_obfuscation_test'
test.dependsOn copySqlObfuscationTest

verifyInstrumentation {
    passesOnly 'com.datastax.cassandra:cassandra-driver-core:[3.8.0,4.0.0)'
}

site {
    title 'Cassandra'
    type 'Datastore, InstanceLevelDB'
}
