buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'

    }
}
apply plugin: 'com.google.protobuf'

def grpcVersion = '1.43.2'
def protobufVersion = '4.28.3'

dependencies {
    implementation(project(":agent-bridge"))
    implementation("io.grpc:grpc-all:${grpcVersion}")
    implementation("com.google.protobuf:protobuf-java:${protobufVersion}")
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.perfmark:perfmark-api:0.23.0")
}

jar {
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.grpc-1.40.0' }
}

verifyInstrumentation {
    passesOnly 'io.grpc:grpc-all:[1.40.0,)'
}

// to generate the proto classes, run ./gradlew generateTestProto
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

test {
    // instrumentation is incompatible with version of java less than 1.8
    onlyIf {
        !project.hasProperty('test7')
    }
}

site {
    title 'gRPC'
    type 'Messaging'
}
