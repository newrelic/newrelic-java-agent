import play.routes.compiler.InjectedRoutesGenerator$
import play.routes.compiler.RoutesCompiler
import play.routes.compiler.RoutesCompiler$
import scala.collection.JavaConversions

apply plugin: 'scala'
scala.zincVersion = "1.2.5"

isScalaProjectEnabled(project, "scala-2.11")

buildscript {
    dependencies {
        classpath 'com.typesafe.play:routes-compiler_2.11:2.6.0'
    }
    repositories {
        mavenCentral()
    }
}

sourceSets.test.scala.srcDir "src/test/java"
sourceSets.test.java.srcDirs = []

compileJava.options.bootstrapClasspath = null

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation(project(":newrelic-api"))
    implementation(project(":agent-bridge"))
    implementation(project(":newrelic-weaver-api"))
    implementation(project(":newrelic-weaver-scala-api"))
    implementation("org.scala-lang:scala-library:2.11.12")
    implementation("com.typesafe.play:play-server_2.11:2.6.0")
    testImplementation("com.typesafe.play:play-test_2.11:2.6.0")
    testImplementation("com.typesafe.play:play-java_2.11:2.6.0")
    testImplementation("com.jayway.restassured:rest-assured:2.7.0")
    testImplementation("javax.xml.bind:jaxb-api:2.3.0")
}

jar {
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.play-2.6' }
}

verifyInstrumentation {
    passesOnly 'com.typesafe.play:play-server_2.12:[2.6.0-M4,2.6.13)'
    passesOnly 'com.typesafe.play:play-server_2.11:[2.6.0-M4,2.6.13)'

    // build snapshots
    excludeRegex '.*-[0-9]{4}-[0-9]{2}-[0-9]{2}-[a-z0-9]{7}$'
}

test {
    onlyIf {
        !project.hasProperty('test7')
    }
}

compileTestScala {
    options.compilerArgs += '-proc:none'

    // this manually compiles the conf/routes file into an Routes.scala file, which is subsequently read on startup by our test application
    doFirst {
        def routeFile = file("src/test/resources/conf/routes")
        def outputDirectory = file("src/test/scala")

        def RoutesCompiler.RoutesCompilerTask routesCompilerTask = new RoutesCompiler.RoutesCompilerTask(
                routeFile, JavaConversions.asScalaBuffer(Collections.<String> emptyList()).toSeq(),
                true, false, false)
        RoutesCompiler$.MODULE$.compile(routesCompilerTask, InjectedRoutesGenerator$.MODULE$, outputDirectory)
    }
}

site {
    title 'Play'
    type 'Appserver'
}