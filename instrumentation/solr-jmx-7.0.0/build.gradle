repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":agent-bridge"))
    implementation(project(":agent-bridge-datastore"))
    implementation(project(":newrelic-api"))
    implementation(project(":newrelic-weaver-api"))

    implementation("org.apache.lucene:lucene-core:7.0.0")
    implementation("org.apache.solr:solr-core:7.0.0")
    // some dependencies of solr-core:7.0.0 are no longer available, so should be provided on lib
    implementation(fileTree(include: ["*.jar"], dir: "lib"))
}

def shouldBuild = fileTree(include: ["*.jar"], dir: "lib").size() > 0

compileJava {
    enabled(shouldBuild)
}

compileTestJava {
    enabled(shouldBuild)
}

tasks.getByName("writeCachedWeaveAttributes").enabled(shouldBuild)

jar {
    enabled(shouldBuild)
    manifest { attributes 'Implementation-Title': 'com.newrelic.instrumentation.solr-jmx-7.0.0' }
}

verifyInstrumentation {
    passesOnly 'org.apache.solr:solr-core:[7.0.0,7.4.0)'

    excludeRegex 'org.apache.solr:solr-core:.*(ALPHA|BETA)+$'
}