import com.nr.builder.publish.PublishConfig

plugins {
    id("maven-publish")
    id("signing")
}

apply from: '../gradle/script/spotbugs-config.gradle'
apply from: '../gradle/script/jacoco.gradle'

jar {
    from("$rootDir/LICENSE")
    manifest {
        attributes 'Implementation-Title': 'New Relic Agent API', 'Implementation-Version': project.version, 'Automatic-Module-Name': 'newrelic.api'
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(GenerateModuleMetadata.class) {
    enabled = false
}

tasks.withType(Javadoc).configureEach {
    javadocTool = javaToolchains.javadocToolFor {
        languageVersion = JavaLanguageVersion.of(11)
    }

    options.addBooleanOption("-frames", true)
    options.addBooleanOption("-no-module-directories", true)
}

PublishConfig.config(
        project,
        "New Relic Java agent API",
        "The public API of the Java agent, and no-op implementations for safe usage without the agent.") { it ->
    it.from(components.java)
}
