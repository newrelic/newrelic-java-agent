import com.nr.builder.publish.PublishConfig

plugins {
    id("maven-publish")
    id("signing")
    id("kotlin")
}

configurations {
    testClasses {
        extendsFrom(testRuntimeOnly)
    }
}

apply from: '../gradle/script/spotbugs-config.gradle'
apply from: '../gradle/script/jacoco.gradle'

repositories {
    // hack to allow us to resolve the ':tools' dependency
    flatDir name: 'javaHomeLib', dirs: jdk8 + "/lib"
}

dependencies {
    implementation(project(":newrelic-weaver-api"))
    implementation(project(":newrelic-weaver-scala-api"))

    implementation("org.ow2.asm:asm-commons:$asmVersion")
    implementation("org.ow2.asm:asm-util:$asmVersion")
    implementation("com.google.guava:guava:30.1.1-jre") {
        transitive = false
    }
    implementation("com.github.ben-manes.caffeine:caffeine:2.9.3")

    // used to invoke agentmain
    testImplementation(files(jdk8 + "/lib/tools.jar"))

    // used to test weaving java 1.4 class files
    testImplementation("org.apache.struts:struts-core:1.3.5")

    //testing kotlin coroutines weaving
    testImplementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.21")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")
}

jar {
    manifest {
        attributes 'Implementation-Title': 'New Relic Weaver', 'Implementation-Version': project.version
    }
}

test {
    jvmArgs '-Djdk.attach.allowAttachSelf=true'

    // instrumentation is incompatible with zulu
    useJUnit {
        excludeCategories 'com.newrelic.test.marker.ZuluIncompatibleTest'
    }
}

// There are some test classes that we want to share with newrelic-agent
task testJar(type: Jar) {
    archiveClassifier.set("test")
    from sourceSets.test.output
}

// add the jar generated by the testJar task to the testClasses dependency
artifacts {
    testClasses testJar
}

java {
    withSourcesJar()
    withJavadocJar()
}

PublishConfig.config(
        project,
        "New Relic Java agent Weaver",
        "The Weaver of the Java agent.") { it ->
    it.from(components.java)
}
